#############
#############
# DEFINE SCHEMA #
#############
#############

define

  agent sub entity,
    abstract,
    owns name,
    plays tenancy:tenant,
    plays tenancy:landlord,
    plays subtenancy:sublandlord,
    plays neighbourship:is-neighbour,
    plays tenancy-dispute:disputing-landlord,
    plays tenancy-dispute:disputing-tenant;

  person sub agent,
    plays birth:birthed-child,
    plays subtenancy:subtenant;

  organisation sub agent;

  corporation sub agent;

  house sub entity,
    owns address,
    plays tenancy:rented-property;

  # RELATIONS
  birth sub relation,
    owns starting-date,
    relates birthed-child;

  neighbourship sub relation,
    relates is-neighbour;

  tenancy sub relation,
    relates landlord,
    relates tenant,
    relates rented-property,
    owns starting-date,
    plays subtenancy:main-tenancy,
    plays tenancy-dispute:disputed-tenancy;

  subtenancy sub tenancy,
    relates sublandlord as landlord,
    relates subtenant as tenant,
    relates main-tenancy;

  dispute sub relation,
    relates party,
    relates disputed-subject,
    owns starting-date,
    abstract;

  # neighbour-dispute sub dispute,
  #   relates disputing-neighbour as party,
  #   relates neighbourship as disputed-subject;
  #   # disputed-subject is inherited from dispute

  tenancy-dispute sub dispute,
    relates disputed-tenancy as disputed-subject,
    relates disputing-landlord as party,
    relates disputing-tenant as party;

  name sub attribute,
    value string;

  address sub attribute,
    value string;

  starting-date sub attribute,
    value datetime;
